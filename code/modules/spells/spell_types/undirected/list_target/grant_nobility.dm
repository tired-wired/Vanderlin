/datum/action/cooldown/spell/undirected/list_target/grant_nobility
	name = "Grant Nobility"
	desc = "Make someone a noble, or strip them of their nobility."
	button_icon_state = "recruit_titlegrant"

	spell_type = NONE
	cooldown_time = 4 MINUTES
	target_radius = 3

/datum/action/cooldown/spell/undirected/list_target/grant_nobility/get_list_targets(atom/center, target_radius)
	var/list/things = list()
	if(target_radius)
		for(var/mob/living/carbon/human/target_mob in view(target_radius, center))
			if(QDELETED(target_mob))
				continue
			if(!target_mob.mind || target_mob.stat != CONSCIOUS)
				continue
			if(!target_mob.get_face_name(null))
				continue
			if(isautomaton(target_mob))
				continue
			things += target_mob

	return things

/datum/action/cooldown/spell/undirected/list_target/grant_nobility/before_cast(mob/living/carbon/human/cast_on)
	. = ..()
	if(. & SPELL_CANCEL_CAST)
		return

	if(HAS_TRAIT(cast_on, TRAIT_NOBLE_POWER))
		var/answer = browser_alert(owner, "[cast_on] already has nobility, strip it?", "[name]", DEFAULT_INPUT_CONFIRMATIONS)
		if(QDELETED(src) || QDELETED(owner) || QDELETED(cast_on) || !can_cast_spell())
			return . | SPELL_CANCEL_CAST
		if(answer == CHOICE_CONFIRM)
			owner.say("I HEREBY STRIP YOU, [uppertext(cast_on.name)], OF NOBILITY!")
			REMOVE_TRAIT(cast_on, TRAIT_NOBLE_POWER, TRAIT_GENERIC)
			cast_on.honorary = null
			cast_on.honorary_suffix = null
		else
			reset_spell_cooldown()
		return . | SPELL_CANCEL_CAST

/datum/action/cooldown/spell/undirected/list_target/grant_nobility/cast(mob/living/carbon/human/cast_on)
	. = ..()
	owner.say("I HEREBY GRANT YOU, [uppertext(cast_on.name)], NOBILITY!")
	ADD_TRAIT(cast_on, TRAIT_NOBLE_POWER, TRAIT_GENERIC)
	cast_on.honorary = cast_on.pronouns == SHE_HER ? "Lady" : "Lord"
